PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX cpmeta: <http://meta.icos-cp.eu/ontologies/cpmeta/>
select * where{
	{
		select ?spec where {
			?spec cpmeta:hasDataLevel ?level .
			filter(?level >= 2)
		}
	}
	?dobj cpmeta:hasObjectSpec ?spec .
	OPTIONAL{
		?dobj cpmeta:wasAcquiredBy ?acq .
		OPTIONAL{ ?acq cpmeta:wasPerformedAt/cpmeta:hasSpatialCoverage/cpmeta:asGeoJSON ?siteGeoJson }
		OPTIONAL{ ?acq cpmeta:hasSamplingPoint [cpmeta:hasLatitude ?samplingLat ; cpmeta:hasLongitude ?samplingLon ] }
		OPTIONAL{?acq prov:wasAssociatedWith [cpmeta:hasLatitude ?stationLat ; cpmeta:hasLongitude ?stationLon ] }
	}
	OPTIONAL{ ?dobj cpmeta:hasSpatialCoverage/cpmeta:asGeoJSON ?ownGeoJson }
}