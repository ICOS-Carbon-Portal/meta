prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix cpmeta: <http://meta.icos-cp.eu/ontologies/cpmeta/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcat: <http://www.w3.org/ns/dcat#>
prefix dct: <http://purl.org/dc/terms/>
prefix cpdcat: <http://meta.icos-cp.eu/resources/dcat/>
construct {
	cpdcat:ICOS a <http://www.oil-e.net/ontology/envri-rm.owl#ResearchInfrastructure> ;
		rdfs:label "ICOS"^^xsd:string .

	?station a <http://envri-eu/dcat#Site> ;
		dct:isPartOf cpdcat:ICOS ;
		dct:title ?stationName ;
		dct:description ?comment ;
		dct:subject <http://inspire.ec.europa.eu/metadata-codelist/TopicCategory/environment> ;
		dct:spatial [
			a dct:Location;
			dct:centroid ?pointWkt
		] .
}
where{
	?station a cpmeta:ES ; cpmeta:hasName ?stationName ;
		cpmeta:hasLatitude ?lat ;
		cpmeta:hasLongitude ?lon .
	optional{?station rdfs:comment ?comment }
	bind(concat('POINT(', ?lat, ' ', ?lon, ')') as ?pointWkt)
}
