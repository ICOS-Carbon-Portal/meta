prefix cpmeta: <http://meta.icos-cp.eu/ontologies/cpmeta/>
construct {
?dobj ?p ?o .
?o ?p2 ?o2 .
} where{
	?spec cpmeta:hasDataTheme <http://meta.icos-cp.eu/resources/themes/atmosphere>;
		cpmeta:hasDataLevel "1"^^xsd:integer;
		cpmeta:hasAssociatedProject <http://meta.icos-cp.eu/resources/projects/icos> .
	?dobj cpmeta:hasObjectSpec ?spec .
	filter not exists {[] cpmeta:isNextVersionOf ?dobj}
	filter not exists {?dobj cpmeta:isNextVersionOf []}
	?dobj cpmeta:hasName ?fileName .
	filter (!contains(?fileName, "JFJ"))
	graph <http://meta.icos-cp.eu/resources/atmprodcsv/> {
		?dobj ?p ?o .
		OPTIONAL{
			?o ?p2 ?o2 .
			FILTER(?p != cpmeta:isNextVersionOf)
		}
	}
}

